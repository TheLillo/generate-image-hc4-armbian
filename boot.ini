ODROIDC4-UBOOT-CONFIG

setenv bootlabel "ARMbian"

setenv board "odroidc4"
setenv display_autodetect "true"
setenv hdmimode "1080p60hz"
setenv monitor_onoff "false" # true or false
setenv overscan "100"
setenv sdrmode "auto" 
setenv voutmode "hdmi"
setenv disablehpd "false"
setenv cec "true"
setenv disable_vu7 "false"
setenv max_freq_a55 "1908"    # 1.908 GHz, default value
setenv maxcpus "4"
setenv enable_wol "0"

# Set load addresses
setenv dtb_loadaddr "0x10000000"
setenv dtbo_addr_r "0x11000000"
setenv loadaddr "0x1B00000"
setenv initrd_loadaddr "0x3700000"

if test "${variant}" = "hc4"; then
       setenv max_freq_a55 "1800"
fi

load mmc ${devno}:1 ${loadaddr} config.ini \
    && ini generic ${loadaddr}
if test "x${overlay_profile}" != "x"; then
    ini overlay_${overlay_profile} ${loadaddr}
fi

setenv condev "console=ttyS0,115200n8"   # on both

## Normal HDMI Monitors
if test "${display_autodetect}" = "true"; then hdmitx edid; fi
if test "${hdmimode}" = "custombuilt"; then setenv cmode "modeline=${modeline}"; fi
if test "${cec}" = "true"; then setenv cec_enable "hdmitx=cec3f"; fi
if test "${disable_vu7}" = "false"; then setenv hid_quirks "usbhid.quirks=0x0eef:0x0005:0x0004"; fi

setenv bootargs "root=/dev/mmcblk0p2"

# Load kernel, dtb and initrd
load mmc ${devno}:1 ${loadaddr} Image
load mmc ${devno}:1 ${dtb_loadaddr} dtb/amlogic/meson-sm1-odroid-${variant}.dtb
load mmc ${devno}:1 ${initrd_loadaddr} uInitrd
fdt addr ${dtb_loadaddr}

if test "x{overlays}" != "x"; then
    fdt resize ${overlay_resize}
    for overlay in ${overlays}; do
        load mmc ${devno}:1 ${dtbo_addr_r} dtb/amlogic/overlay/${overlay}.dtbo \
            && fdt apply ${dtbo_addr_r}
    done
fi

load mmc ${devno}:1 ${dtbo_addr_r} dtb/amlogic/overlay/meson-i2cA.dtbo \
    && fdt apply ${dtbo_addr_r}

load mmc ${devno}:1 ${dtbo_addr_r} dtb/amlogic/overlay/meson-i2cB.dtbo \
    && fdt apply ${dtbo_addr_r}

# boot
booti ${loadaddr} ${initrd_loadaddr} ${dtb_loadaddr}
